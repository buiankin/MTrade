
import org.gradle.internal.os.OperatingSystem

plugins {
    id 'com.android.application'
}

android {

    signingConfigs {
        config {
            keyAlias 'androiddebugkey'
            switch (OperatingSystem.current()) {
                case OperatingSystem.LINUX:
                    storeFile file('/home/denis/Android/mykeys/debug.keystore')
                    break
                case OperatingSystem.WINDOWS:
                    storeFile file('c:/Android/workspace/keys/debug.keystore')
                    break
            }
            keyPassword 'android'
            storePassword 'android'
        }
        release
    }

    bundle {
        storeArchive {
            enable = false
        }
    }

    defaultConfig {
        applicationId "ru.code22.mtrade"
        minSdkVersion 24
        targetSdkVersion 34
        compileSdk 34
        versionCode 3108
        versionName "3.108"
        //testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        ndk{
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }
        vectorDrawables {
            useSupportLibrary = true
        }
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            signingConfig signingConfigs.config
            debuggable true
        }

    }


    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }
    packagingOptions {
        jniLibs {
            pickFirsts += ['lib/armeabi-v7a/libmain.so', 'lib/mips/libmain.so', 'lib/x86/libmain.so']
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        viewBinding true
    }
    ndkVersion '25.0.8775105'
    lint {
        abortOnError false
        checkReleaseBuilds false
    }
    namespace 'ru.code22.mtrade'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.fragment:fragment:1.8.4'

    implementation 'com.android.support:multidex'

    implementation 'commons-net:commons-net:3.10.0'
    implementation 'commons-io:commons-io:2.15.0'

    //implementation 'com.github.TellH:RecyclerTreeView:1.2.0'

    //implementation files('libs/commons-net-3.6.jar')
    //implementation files('libs/jheader-0.1.jar')
    //implementation files('libs/ksoap2-android-assembly-3.3.0-jar-with-dependencies.jar')
    //implementation files('libs/universal-image-loader-1.9.5.jar')
    // чуть не воспользовался WildcardFileFilter от апача из commons.io, но там минимальные требования api 24
    // поэтому ставим версию 2.6 (я ее скопировал в lib)
    //implementation "commons-io:commons-io:2.7"

    //implementation 'android.arch.work:work-runtime:1.0.1'
    implementation 'androidx.work:work-runtime:2.9.1'
    implementation 'androidx.preference:preference:1.2.1'

    // хотя пока не используется
    implementation 'androidx.exifinterface:exifinterface:1.3.7'

    implementation 'com.squareup.picasso:picasso:2.71828'
    //implementation 'org.jetbrains:annotations-java5:15.0'

    //implementation 'com.google.android.play:core:1.10.3'
    implementation 'com.google.android.play:app-update:2.1.0'

    implementation 'com.google.code.ksoap2-android:ksoap2-android:3.6.2'

    //implementation "androidx.concurrent:concurrent-futures:1.1.0"
    implementation 'com.google.guava:guava:32.0.1-jre'

    implementation 'com.squareup.okhttp3:okhttp:3.10.0'

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.2'

    //testImplementation 'junit:junit:4.13.2'
    //androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    //androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'



}

// Add a file signing.properties
File propFile = file('signing.properties')
if (propFile.exists()) {
    Properties props = new Properties()
    props.load(new FileInputStream(propFile))

    if (props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
            props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
        android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
    } else {
        android.buildTypes.release.signingConfig = null
    }
} else {
    android.buildTypes.release.signingConfig = null
}

configurations {
    all {
        exclude group: 'androidx.lifecycle', module: 'lifecycle-viewmodel-ktx'
    }
    cleanedAnnotations
    compile.exclude group: 'org.jetbrains' , module:'annotations'
}